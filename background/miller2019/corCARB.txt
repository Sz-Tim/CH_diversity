
model {

    # MV CAR prior for the spatial random effects 
    # MVCAR prior 
    
    S[1:2, 1:ncell] ~ mv.car(adj[], weights[], num[], omega[ , ])
    
    # Other priors
    for (k in 1:2) {
    alpha[k] ~ dflat()
    }
    # Precision matrix of MVCAR 
    omega[1:2, 1:2] ~ dwish(R[ , ], 2)
    
    # Covariance matrix of MVCAR   
    sigma2[1:2, 1:2] <- inverse(omega[ , ])
    
    # conditional SD of S[1, ] 
    sigma[1] <- sqrt(sigma2[1, 1])
    
    # conditional SD of S[2,] 
    sigma[2] <- sqrt(sigma2[2, 2])
    
    # within-area conditional correlation 
    corr <- sigma2[1, 2] / (sigma[1] * sigma[2])
    
    # double-check mean = 0
    mean1 <- mean(S[1,])
    mean2 <- mean(S[2,])
    
    
    ### BBA model
    ### priors
    
    p[1] ~ dunif(0,1)
    p[2] ~ dunif(0,1)
    b.forest ~ dnorm(0,0.01)
    b.elev ~ dnorm(0,0.01)
    b.effort[1] ~ dunif(0,1000)
    b.effort[2] ~ dunif(0,1000)
    
    ### data model
    
    for (i in 1:nsite){
      z[i] ~ dbern(psi[cell[i]])
      muy[i] <- z[i]*p[1]
      Y[i] ~ dbin(muy[i],5)
    }
    
    ### eBird Model
    ### Data Model
    
    for (j in 1:ncell){
      logit(psi[j]) <- S[1,j] + alpha[1] + b.forest*forest[j] + b.elev*elev[j]
      logit(delta[j]) <- S[2,j] + alpha[2]
      E[j] <- effort[j,1] 
      muP[j] <- delta[j] * (1 - pow((1-p[2]),E[j]))
      W[j] ~ dbern(muP[j])
    }
  }
    
    
